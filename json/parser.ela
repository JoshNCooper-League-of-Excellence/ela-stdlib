type JsonResult :: Result!<*mut JsonValue, String>;

fn parse(lexer: *mut JsonLexer) -> JsonResult {
  mut token_result := lexer.expect_types(.[
    TokenType::LBracket,
    TokenType::LCurly,
    TokenType::Number,
    TokenType::String,
    TokenType::True,
    TokenType::False,
    TokenType::Null,
  ]);

  if token_result.is_err()
    then return token_result.propagate();

  value : *mut JsonValue = new(JsonValue.{
    _type: JsonValueType::Invalid,
  });

  mut token := token_result.unwrap();

  switch token._type {
    TokenType::LBracket: {
      value._type = JsonValueType::Array;
      value.array = .{};

      token = lexer.peek();

      if token._type == TokenType::RBracket {
        lexer.eat();
        return JsonResult::Ok(value);
      }

      while token._type != TokenType::RBracket {
        value_result := parse(lexer);
        if value_result.is_err()
          then return value_result.propagate();

        value.array.push(value_result.unwrap());

        token_result = lexer.expect_types(.[
          TokenType::RBracket, 
          TokenType::Comma
        ]);
        if token_result.is_err()
          then return token_result.propagate();
        token = token_result.unwrap();
      }
    }
    TokenType::LCurly: {
      value._type = JsonValueType::Object;
      value.object = .{};

      if lexer.peek()._type == TokenType::RCurly {
        lexer.eat();
        return JsonResult::Ok(value);
      }

      while token._type != TokenType::RCurly {
        
        token_result = lexer.expect_type(TokenType::String);
        if token_result.is_err() 
          then return token_result.propagate();
        
        key_token := token_result.unwrap();

        token_result = lexer.expect_type(TokenType::Colon);
        if token_result.is_err() 
          then return token_result.propagate();

        value_result := parse(lexer);
        if value_result.is_err() 
          then return value_result.propagate();
        
        val := value_result.unwrap();

        value.object.insert(key_token.value, val);

        token_result = lexer.expect_types(.[
          TokenType::RCurly, 
          TokenType::Comma
        ]);

        if token_result.is_err() 
          then return token_result.propagate();

        token = token_result.unwrap();
      }
    }
  }
  
  if token._type == TokenType::Number {
    value._type = JsonValueType::Number;
    value.number = std::c::atof(token.value.data);
  } else if token._type == TokenType::String {
    value._type = JsonValueType::String;
    value.string = token.value;
  } else if token._type == TokenType::True {
    value._type = JsonValueType::Boolean;
    value.boolean = true;
  } else if token._type == TokenType::False {
    value._type = JsonValueType::Boolean;
    value.boolean = false;
  } else if token._type == TokenType::Null {
    value._type = JsonValueType::Null;
  }

  return JsonResult::Ok(value);
}

fn parse_file(file: str) -> JsonResult {
  result := File::read_all(file);

  if result is Result!<String, String>::Err(err) {
    return JsonResult::Err(err));
  }

  mut lexer := JsonLexer::create(file, result.unwrap());
  return parse(&mut lexer);
}

fn parse_string(s: str) -> JsonResult {
  mut lexer := JsonLexer::create(":<loaded from string>:".to_string(get_global_allocator()), s.to_string(get_global_allocator()));
  return parse(&mut lexer);
}