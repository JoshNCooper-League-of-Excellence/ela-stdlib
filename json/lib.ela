#include "lexer.ela"
#include "value.ela"
#include "parser.ela"
#include "serialize.ela"
#include "deserialize.ela"

impl Serializable for String {
  fn serialize(*const self) -> JsonResult {
    mut jstring := JsonValue::new();
    jstring.type = JsonValueType::String;
    jstring.string = self.clone();
    return JsonResult::Ok(jstring);
  }

  fn deserialize(*mut self, object: *mut JsonValue) -> Option!<String> {
    *self = object.string.clone();
    return None();
  }
}

impl!<T> Serializable for List!<T> {
  fn serialize(*const self) -> JsonResult {
    return serialize_iterator!<Iter!<T>, T>(self.iter());
  }

  fn deserialize(*mut self, object: *mut JsonValue) -> Option!<String> {
    if object.type != JsonValueType::Array then return Some(String::from("invalid json type, expected array."));
    self.initialize(object.array.length);
    for i in 0..object.array.length {
      err := deserialize(object.array[i], &mut (*self)[i]);
      if err.is_some() {
        return err;
      }
    }
    return .{};
  }
}

// We need a way to do this for any key value, we have to constrain it to !<String, V>
// in some way, but using a where will just make it so you cannot use Map!<> with anything but String
// in a project where you are also using Json.
impl!<K, V> Serializable for Map!<K, V> {
  fn serialize(*const self) -> JsonResult {
    mut value := JsonValue::new();
    value.type = JsonValueType::Object;
    for key, v in *self {
      result := serialize!<V>(v);
      if result.is_err() {
        return result;
      }
      value.object.insert(key, result.unwrap());
    }
    return JsonResult::Ok(value);
  }
  fn deserialize(*mut self, object: *mut JsonValue) -> Option!<String> {
    println(object.type as JsonValueType);

    if object.type != JsonValueType::Object 
      then return Some(String::from("invalid json type, expected an object to deserialize into Map."));

    for key, value in object.object {
      mut v: V;
      err := deserialize!<V>(value, &mut v);
      if err.is_some() {
        return err;
      }
      self.insert(key, v);
    }

    return None();
  }
}
