import fmt::{
  FormatOptions,
  StringBuilder,
  Format,
};

import map::*;

enum JsonValueType {
  Invalid = -1,
  String,
  Number,
  Object,
  Array,
  Boolean,
  Null,
}

// TODO: this should just be a choice _type.
struct JsonValue {
  _type : s32,
  union {
    number: f64,
    boolean: bool,

    // these need to be freed.
    string: String,
    array: List!<*mut JsonValue>,
    object: Map!<String, *mut JsonValue>,
  },
}

impl Destroy for JsonValue {
  fn destroy(*mut self, recursive: bool = true) {
    switch self._type {
      JsonValueType::Object: {
        for key, value in self.object {
          key.destroy();
          value.destroy();
          std::c::free(value);
        }
        self.object.destroy();
      }
      JsonValueType::Array: {
        for val in self.array {
          val.destroy();
          std::c::free(val);
        }
        self.array.destroy();
      }
      JsonValueType::String: {
        self.string.destroy();
      }
    }
  }
}

impl Subscript!<JsonValue> for JsonValue {
  fn subscript(*const self, index: u64) -> *const JsonValue {
    if self._type != JsonValueType::Array
      then return null;

    if index < 0 || index >= self.array.length
      then return null;

    return self.array[index];
  }

  fn subscript_mut(*mut self, index: u64) -> *mut JsonValue {
    if self._type != JsonValueType::Array
      then return null;
    if index < 0 || index >= self.array.length
      then return null;
    return self.array[index];
  }
}

struct Writer {
  builder: StringBuilder,
  options: FormatOptions,
  indentation: (u16 /* depth */, u8 /* width in spaces */),
}

impl Writer {
  fn new() -> #self {
    options := FormatOptions.{
      indented: true,
      quote_strings: true,
      // TODO: JSON spec doesn't preserve formatting of digits, it trims all trailing zeros.
      // TODO: We need a way to tell our format options to do this, it's nice anyway
      digits_after_decimal: 8,
    };
    return .{
      builder: .{},
      options: options,
      indentation: (0, 2),
    }
  }

  fn indent(*mut self, depth_change: s8) {
    if depth_change > 0  {
      self.indentation.0 += depth_change;
    } else if depth_change < 0 {
      self.indentation.0 += depth_change;
      if self.indentation.0 < 0
        then self.indentation.0 = 0;
      return;
    }

    depth, width := self.indentation;
    if depth > 0 && width > 0 {
      mut indent: String;
      for i in 0..(depth * width) {
        indent.push(' ');
      }
      self.builder.append_then_free(indent);
    }
  }

  fn newline(*mut self) {
    if self.options.indented {
      self.builder.append_byte('\n');
      self.indent(0);
    }
  }


  fn write!<DataSlice>(*mut self, data: DataSlice) where DataSlice: AsByteSlice {
    self.builder.append(data);
  }

  fn writef!<Tuple>(*mut self, fmt: str, args: Tuple) where Tuple: IsTuple {
    self.builder.appendf(fmt, args, self.options);
  }

  fn write_byte(*mut self, byte: u8) {
    self.builder.append_byte(byte);
  }
}

impl Destroy for Writer {
  fn destroy(*mut self, recursive: bool = true) {
    self.builder.destroy();
  }
}

impl JsonValue {
  fn to_string(*const self, writer: *mut Writer) {
    switch self._type {
      JsonValueType::Object: {
        writer.write_byte('{');
        writer.indent(1);
        writer.newline();
        mut idx := 0;
        for key, value in self.object {
          if idx != 0 {
            writer.write(", ");
            writer.newline();
          }
          idx++;
          writer.writef("%", (key,));
          writer.write(": ");
          value.to_string(writer);
        }
        writer.indent(-1);
        writer.newline();
        writer.write_byte('}');
      }
      JsonValueType::Array: {
        writer.write_byte('[');
        writer.indent(1);
        writer.newline();
        mut idx := 0;
        for val in self.array {
          if idx != 0 {
            writer.write(", ");
            writer.newline();
          }
          idx++;
          val.to_string(writer);
        }
        writer.indent(-1);
        writer.newline();
        writer.write_byte(']');
      }
      JsonValueType::Number: {
        writer.writef("%", (self.number,));
      }
      JsonValueType::String: {
        writer.writef("%", (self.string,));
      }
      JsonValueType::Boolean: {
        value := switch self.boolean {
          true => "true",
          false => "false"
        };
        writer.write(value);
      }
      JsonValueType::Null: {
        writer.write("null");
      }
      else: {
        panic("unsupported json _type, probably a corrupted JsonValue");
      }
    }
  }
}

impl JsonValue {
  fn stringify(*const self) -> String {
    mut writer := Writer::new();
    self.to_string(&mut writer);
    data := writer.builder.get_string();
    writer.destroy();
    return data;
  }

  fn get(*mut self, key: String) -> Option!<*mut JsonValue> {
    if self._type != JsonValueType::Object {
      return None();
    }
    return self.object.get(key);
  }

  fn is_number(*mut self)  -> bool
    => self._type == JsonValueType::Number;

  fn is_string(*mut self)  -> bool
    => self._type == JsonValueType::String;

  fn is_object(*mut self)  -> bool
    => self._type == JsonValueType::Object;

  fn is_array(*mut self)   -> bool
    => self._type == JsonValueType::Array;

  fn is_boolean(*mut self) -> bool
    => self._type == JsonValueType::Boolean;

  fn is_null(*mut self)    -> bool
    => self._type == JsonValueType::Null;

  fn as_number(*mut self) -> Option!<f64> {
    if self._type == JsonValueType::Number {
      return Some(self.number);
    }
    return None();
  }

  fn as_string(*mut self) -> Option!<String> {
    if self._type == JsonValueType::String {
      return Some(self.string);
    }
    return None()
  }

  fn as_object(*mut self) -> Option!<Map!<String, *mut JsonValue>> {
    if self._type == JsonValueType::Object {
      return Some(self.object);
    }
    return None();
  }

  fn as_array(*mut self) -> Option!<List!<*mut JsonValue>> {
    if self._type == JsonValueType::Array {
      return Some(self.array);
    }
    return None();
  }

  fn as_boolean(*mut self) -> Option!<bool> {
    if self._type == JsonValueType::Boolean {
      return Some(self.boolean);
    }
    return None();
  }

  fn new() -> *mut JsonValue {
    ptr: *mut JsonValue = std::c::malloc(sizeof(JsonValue));
    *ptr = .{};
    return ptr;
  }
}

impl Format for JsonValue {
  fn format(*const self, formatter: *mut Formatter) {
    mut writer := Writer::new();
    writer.builder = *formatter.writer;
    self.to_string(&mut writer);
    *formatter.writer = writer.builder;
  }
}