#flags ArenaFlags :: enum {
  None,               // default.
  AlignAllocations,   // if self flag is set, the allocator will fix alignment automatically when a misaligned allocation is found. Else it will naively allocate regardless of size.
}

impl ArenaFlags {
  default :: fn() -> #self {
    return #self::None;
  }
}

// Super basic arena allocator, with a constructor that allocates a big block,
// an allocate method that returns u8*, and a destructor that frees the block.
Arena :: struct {
  ptr: u64,
  size: u64,
  memory: u8*,
  flags: ArenaFlags,
  alignment: u64,
  next: Arena*,
}

impl Deinit for Arena {
  deinit :: fn(self*) {
    if self.next then self.next.deinit();
    free(self.memory);
    self.memory = null;
  }
}

impl Arena {
  new :: fn(size: u64, flags: ArenaFlags) -> #self {
    return .{
      size: size,
      flags: flags,
      memory: calloc(sizeof(u8), size),
      alignment: sizeof(void*),
      next: null,
    }
  }

  alloc :: fn(self*, size: u64) -> u8* {
    if size == 0 {
      return null;
    }

    if size > self.size {
      buffer: u8[1024];
      len := snprintf(
        buffer, 
        1024, 
        "attempted to allocate %d bytes in an arena where each chunk is maximum %d bytes. this would lead to splits in the data."c, 
        size, self.size
      );
      panic(.{ data: buffer, length: len });
    }
    
    aligned_ptr := self.ptr;
    if self.flags & ArenaFlags::AlignAllocations != 0 {
      aligned_ptr = (self.ptr + self.alignment - 1) & ~(self.alignment - 1);
    }
    
    oom := aligned_ptr + size > self.size;
    
    if oom  {
      if !self.next {
        self.next = new(#self::new(self.size, self.flags))
      }
      return self.next.alloc(size);
    }
    
    result := self.memory + aligned_ptr;
    self.ptr = aligned_ptr + size;
    return result;
  }

}

impl Allocator for Arena {
  
  allocate :: fn!<T>(self*) -> T* {
    return self.alloc(sizeof(T));
  }
  allocate_array :: fn!<T>(self*, n: u64) -> T* {
    return self.alloc(sizeof(T) * n);
  }

  // these do nothing in our arena.
  // maybe we could provide resize and just blindly reallocate a whole new chunk?
  // i have no idea how we'd track that though
  free :: fn!<T>(self*, ptr: T*) {}
  resize :: fn!<T>(self*, ptr: T*, new_size: u64) -> T* {
    // not sure how we'd do this without knowing how big that allocation used to be.
  }

  copy :: fn!<T>(self*, ptr: T*, n: u64) -> T* {
    new_ptr := self.alloc(sizeof(T) * n);
    memcpy(new_ptr, ptr, n);
    return new_ptr;
  }
}