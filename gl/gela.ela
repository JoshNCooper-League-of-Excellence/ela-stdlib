Shader :: struct {
  handle: u32
}

impl Shader {
  compile :: fn(kind: u32, source: u8*) -> u32 {
    shader: u32 = glCreateShader(kind);
    glShaderSource(shader, 1, &source, null);
    glCompileShader(shader);

    success: s32;
    glGetShaderiv(shader, GL::COMPILE_STATUS, &success);
    if success == 0 {
      length: s32;
      glGetShaderiv(shader, GL::INFO_LOG_LENGTH, &length);
      info_log: u8* = malloc(sizeof(u8*) * length);
      glGetShaderInfoLog(shader, length, &length, info_log);
      printf("Shader compilation failed: %s\n"c, info_log);
      free(info_log);
      return 0;
    }
    return shader;
  }

  compile_program :: fn(vert_source: u8*, frag_source: u8*) -> u32 {
    vertex_shader: u32 = Shader::compile(GL::VERTEX_SHADER, vert_source);
    fragment_shader: u32 = Shader::compile(GL::FRAGMENT_SHADER, frag_source);

    if vertex_shader == 0 || fragment_shader == 0 {
      return 0 as u32;
    }

    program: u32 = glCreateProgram();
    glAttachShader(program, vertex_shader);
    glAttachShader(program, fragment_shader);
    glLinkProgram(program);

    success: s32;
    glGetProgramiv(program, GL::LINK_STATUS, &success);
    if success == 0 {
      length: s32;
      glGetProgramiv(program, GL::INFO_LOG_LENGTH, &length);
      info_log: u8* = malloc(sizeof(u8*) * length);
      glGetProgramInfoLog(program, length, &length, info_log);
      printf("Program linking failed: %s\n"c, info_log);
      free(info_log);
      return 0 as u32;
    }

    glDeleteShader(vertex_shader);
    glDeleteShader(fragment_shader);

    return program;
  }



  use :: fn(self*) {
    glUseProgram(self.handle);
  }
  
  create :: fn(vertexSource: String, fragSource: String) -> #self {
    v: #self;
    v.handle = #self::compile_program(vertexSource.data, fragSource.data);
    return v;
  }

  set_mat4 :: fn(self*, name: str, v: Mat4*) {
    index := glGetUniformLocation(self.handle, name.data);
    if index != -1 {
      glUniformMatrix4fv(index, 1, false, v.values as f32*);
    } else {
      printf("failed to get uniform : %s\n"c, name.data);
    }
  }
  
  set_vec4 :: fn(self*, name: str, v: Vec4*) {
    index := glGetUniformLocation(self.handle, name.data);
    if index != -1 {
      glUniform4fv(index, 1, v.values as f32*);
    } else {
      printf("failed to get uniform : %s\n"c, name.data);
    }
  }
  
  set_vec3 :: fn(self*, name: str, v: Vec3*) {
    index := glGetUniformLocation(self.handle, name.data);
    if index != -1 {
      glUniform3fv(index, 1, v.values as f32*);
    } else {
      printf("failed to get uniform : %s\n"c, name.data);
    }
  }
  
  set_vec2 :: fn(self*, name: str, v: Vec2*) {
    index := glGetUniformLocation(self.handle, name.data);
    if index != -1 {
      glUniform2fv(index, 1, v.values as f32*);
    } else {
      printf("failed to get uniform : %s\n"c, name.data);
    }
  }
  
  set_float :: fn(self*, name: str, v: f32) {
    index := glGetUniformLocation(self.handle, name.data);
    if index != -1 {
      glUniform1f(index, v);
    } else {
      printf("failed to get uniform : %s\n"c, name.data);
    }
  }

  set :: fn![T](self*, name: str, v: T*) {
    switch typeof(T) {
      typeof(f32): {
        self.set_float(name, *(v as f32*));
        return;
      }
      typeof(Vec2): {
        self.set_vec2(name, v as Vec2*);
        return;
      }
      typeof(Vec3): {
        self.set_vec3(name, v as Vec3*);
        return;
      }
      typeof(Vec4): {
        self.set_vec4(name, v as Vec4*);
        return;
      }
      typeof(Mat4): {
        self.set_mat4(name, v as Mat4*);
        return;
      }
    }
    printf("incompatible type with 'set' method on shader: only f32, Vec2/3/4, and Mat4 are currently supported\n");
  }
}

impl Deinit for Shader {
  deinit :: fn(self*) {
    glDeleteProgram(self.handle);
  }
}

Window :: struct {
  handle: GLFWwindow*
}

impl Window {
  create :: fn(width: s32, height: s32, title: str) -> #self {
    self: #self;
    
    if !glfwInit() {
      printf("Failed to initialize glfw\n"c);
      return self;
    }
    
    self.handle = glfwCreateWindow(width, height, title.data, null, null);
    
    if self.handle == null {
      printf("Failed to create window\n"c);
      glfwTerminate();
      return self;
    }
    
    glfwMakeContextCurrent(self.handle);
    
    if glewInit() != 0 {
      printf("Failed to initialize glew\n"c);
      glfwTerminate();
      return self;
    }
    
    return self;
  }
  
  should_close :: fn(self*) -> bool {
    return glfwWindowShouldClose(self.handle) as bool;
  }
  
  present :: fn(self*) {
    glfwSwapBuffers(self.handle);
    glfwPollEvents();
  }
  
  clear :: fn(flags: GL) {
    glClear(flags);
  }


  set_key_callback :: fn(self*, callback: fn*(GLFWwindow*, s32, s32, s32, s32)) {
    glfwSetKeyCallback(self.handle, callback);
  }
}

Camera :: struct {
  position: Vec3,
  target: Vec3,
  up: Vec3,
  fov: f32,
  aspect_ratio: f32,
  near: f32,
  far: f32,
  view: Mat4,
  projection: Mat4,
}

impl Camera {
  new :: fn(up: Vec3, fov: f32, aspect, near, far) -> #self {
    return .{
      position: Vec3::zero(),
      target:   Vec3::zero(),
      up: up,
      fov: fov,
      aspect_ratio: aspect,
      near: near,
      far: far,
    }
  }

  update :: fn(self*) {
    self.view = Mat4::look_at(self.position, self.target, self.up);
    self.projection = Mat4::perspective(self.fov, self.aspect_ratio, self.near, self.far);
  }
}