const EOF := -1;

struct _IO_FILE;
type FILE :: _IO_FILE;

struct Time_Spec {
  tv_sec: s64,
  tv_nsec: s64,
};

struct Stat {
  st_dev: u64,
  st_ino: u64,
  st_nlink: u64,
  st_mode: u32,
  st_uid: u32,
  st_gid: u32,
  __pad0: s32,
  st_rdev: u64,
  st_size: u64,
  st_blksize: s64,
  st_blocks: s64,
  st_atim: Time_Spec,
  st_mtim: Time_Spec,
  st_ctim: Time_Spec,
  __glibc_reserved: [s64; 3],
};

extern fn fstat(fd: s32, stat: *mut Stat) -> s32;
extern fn fileno(file: *const FILE) -> s32;

/* Idk the validity of these methods, just written by chatgpt */
impl Stat {
  fn new!<T>(path: T) -> Option!<Self> where T: AsSlice!<u8> {
    file := fopen(path.as_slice().data, "r"c);

    if !file {
      return Option!<Self>::None;
    }

    mut stat: Self;
    fd := fileno(file);

    if fd == -1 {
      fclose(file);
      return Option!<Self>::None;
    }

    fstat(fd, &mut stat);
    fclose(file);
    return Option!<Self>::Some(stat);
  }

  fn is_dir(*const self) -> bool {
      return (self.st_mode & 0xF000) == 0x4000;
  }
  
  fn is_file(*const self) -> bool {
      return (self.st_mode & 0xF000) == 0x8000;
  }
  
  fn is_symlink(*const self) -> bool {
      return (self.st_mode & 0xF000) == 0xA000;
  }
  
  fn is_block_device(*const self) -> bool {
      return (self.st_mode & 0xF000) == 0x6000;
  }
  
  fn is_char_device(*const self) -> bool {
      return (self.st_mode & 0xF000) == 0x2000;
  }
  
  fn is_fifo(*const self) -> bool {
      return (self.st_mode & 0xF000) == 0x1000;
  }
  
  fn is_socket(*const self) -> bool {
      return (self.st_mode & 0xF000) == 0xC000;
  }
}

enum SeekPosition {
  // Seek from beginning of file.
  Begin,
  // Seek from current position.
  Current,
  // Seek from end of file.
  End,
}

extern fn fread(ptr: *mut void, size: u64, count: u64, stream: *mut FILE) -> u64;
extern fn fwrite(ptr: *const void, size: u64, count: u64, stream: *mut FILE) -> u64;
extern fn fseek(stream: *mut FILE, offset: s64, origin: s32) -> s32;
extern fn ftell(stream: *const FILE) -> s64;
extern fn rewind(stream: *mut FILE) -> void;
extern fn fopen(fname: *const u8, flags: *const u8) -> *mut FILE;
extern fn fclose(file: *mut FILE) -> void;
extern fn fgetc(file: *mut FILE) -> s8;
extern fn putc(ch: u8, file: *mut FILE) -> void;
extern fn fprintf(stream: *mut FILE, format: c_str, ...) -> s32;
extern fn fflush(f: *const FILE);
extern fn mkdir(path: *const u8, mode: u32) -> s32;


// TODO: add a way to do Binary stuff.
#flags enum FileOpenFlags {
  Read,
  Write,
}

impl FileOpenFlags {
  // Used to convert our flag enum to a str for fopen/fclose etc.
  // this does not ever need to be freed.
  fn as_str(self) -> c_str {
    if self == FileOpenFlags::Read {
      return "r"c;
    } else if self == FileOpenFlags::Write {
      return "w"c;
    } else if self == FileOpenFlags::Read | FileOpenFlags::Write {
      return "rw"c;
    } else {
      panic("unexpected file flags passed to a file api function");
      return null;
    }
  }
}

struct File {
  fd: *mut FILE,
}

impl File {
  fn delete!<PathT>(path: PathT) -> bool where PathT: AsSlice!<u8> {
    extern fn remove(path: *const u8) -> s32;
    slice := path.as_slice();
    if slice.length == 0 {
      return false;
    }
    return remove(slice.data) == 0;
  }

  fn copy!<SourcePathT, DestPathT>(source: SourcePathT, dest: DestPathT) -> Result!<Infallible, String>
    where SourcePathT: AsSlice!<u8>, DestPathT: AsSlice!<u8> {
    type Result :: Result!<Infallible, String>;
    src := source.as_slice();
    dst := dest.as_slice();

    in_file := fopen(src.data, "r"c);
    if !in_file {
      return Result::Err(String::from("Unable to open source file"));
    }

    out_file := fopen(dst.data, "w"c);
    if !out_file {
      fclose(in_file);
      return Result::Err(String::from("Unable to open destination file"));
    }

    mut buf: [u8; 8192];

    while {
      read := fread(buf, sizeof(u8), 8192, in_file);
      if read == 0 {
        break;
      }
      written := fwrite(buf, sizeof(u8), read, out_file);
      if written != read {
        fclose(in_file);
        fclose(out_file);
        return Result::Err(String::from("Failed to write all bytes to destination"));
      }
    }

    fflush(out_file);
    fclose(in_file);
    fclose(out_file);

    return Result::Ok(Infallible.{});
  }

  fn stat(*mut self) -> Option!<Stat> {
    type Option :: Option!<Stat>;
    mut stat: Stat;
    if fstat(fileno(self.fd), &mut stat) != 0 {
      return Option::None;
    }
    return Option::Some(stat);
  }

  fn exists!<PathT>(path: PathT) -> bool where PathT: AsSlice!<u8> {
    return Stat::new(path).is_some();
  }

  fn open!<T>(path: T, flags: FileOpenFlags) -> Self where T: AsSlice!<u8> {
    slice := path.as_slice();
    mut self: Self;
    self.fd = fopen(slice.data, flags.as_str());
    return self;
  }

  fn write_raw(*mut self, data: *u8, length: u64) -> u64 {
    return fwrite(data, sizeof(u8), length, self.fd);
  }

  fn write!<Data>(*mut self, data: Data) -> u64 where Data: AsSlice!<u8> {
    slice := data.as_slice();
    return fwrite(slice.data, sizeof(u8), slice.length, self.fd);
  }

  fn read(*mut self, buffer: *mut u8, size: u64) -> u64 {
    return fread(buffer, sizeof(u8), size, self.fd);
  }


  fn flush(*mut self) {
    fflush(self.fd);
  }

  fn close(*const self) -> void {
    fflush(self.fd);
    fclose(self.fd);
  }

  fn length(file: *mut FILE) -> u32 {
    original_pos := ftell(file);
    fseek(file, 0, SeekPosition::End);
    len := ftell(file);
    fseek(file, original_pos, SeekPosition::Current);
    return len as u32;
  }

  fn read_all!<Path>(filename_path: Path) -> Result!<String, str> where Path: AsSlice!<u8> {
    type Result :: Result!<String, str>;
    filename := filename_path.as_slice();
    file := fopen(filename.data, "r"c);

    if !file then 
      return Result::Err("Couldnt open file");

    mut string: String;
    string.resize(1024);
    while {
      c := fgetc(file);
      if c == EOF then break;
      string.push(c);
    }
    fclose(file);
    return Result::Ok(string);
  }
  
  fn read_bytes!<Path>(path: Path) -> Result!<List!<u8>, str> where Path: AsSlice!<u8> {
    type Result :: Result!<List!<u8>, str>;
    filename := path.as_slice();
    file := fopen(filename.data, "r"c);
    if !file then return Result::Err("Couldnt open file");
    mut bytes: List!<u8>;
    bytes.resize(1024);

    while {
      c := fgetc(file);
      if c == EOF then break;
      bytes.push(c);
    }
    
    fclose(file);
    return Result::Ok(bytes);
  }
  
  fn write_all!<Path, Data>(path: Path, data_slice: Data) -> Option!<str> 
    where Path: AsSlice!<u8>, Data: AsSlice!<u8> {
    type Option :: Option!<str>;

    filename := path.as_slice();

    data := data_slice.as_slice();

    file := fopen(filename.data, "w "c);

    if !file {
      return Option::Some("Unable to open file");
    }

    fwrite(data.data, sizeof(u8), data.length, file);

    fclose(file);

    return Option::None;
  }

  fn read_line(*mut self) -> String {
    mut line: String;
    while {
      c := fgetc(self.fd);
      if c == EOF || c == '\n' {
        break;
      }
      line.push(c);
    }
    return line;
  }
}

impl Destroy for File {
  fn destroy(*mut self, recursive: bool = true) {
    self.close();
  }
}


/* 
  ! We should make a solid decision if we want the free functions like the ones below
  ! to be within a struct as associated functions, or if we want to keep them free functions
  ! floating in our module. I think I prefer the latter, but it's easy to change for now.

  ! I just copied this from my system, we need a better way to get this.
  ! should use pathconf.
*/
const PATH_MAX := 4096;

extern fn realpath(path: *const u8, result: *mut u8) -> *const u8;

fn canonical_path!<Path>(path: Path) -> Option!<String> where Path: AsSlice!<u8> {
  slice := path.as_slice();
  if slice.length == 0 {
    return None();
  }
  mut resolved_path: [u8; PATH_MAX];
  if !realpath(slice.data, resolved_path) {
    return None();
  }
  return Some(String::from_ptr(resolved_path));
}

extern fn chdir(path: *const u8) -> s32;
extern fn dirname(path: *const u8) -> *mut u8;

/* 
  The option return _type simply says when it .is_none(), it has failed in some capacity.
  We don't have a way to interact with perror, so we don't really get much information as to why
  this has failed, just that it did fail.
*/
fn change_directory!<Path>(path: Path) -> bool where Path: AsSlice!<u8> {
  slice := path.as_slice();
  if slice.length == 0 {
    return false;
  }
  if chdir(slice.data) != 0 {
    return false;
  }
  return true;
}

fn parent_path!<Path>(path: Path) -> Option!<String> where Path: AsSlice!<u8> {
  slice := path.as_slice();
  if slice.length == 0 {
    return None();
  }
  mut data := std::c::strdup(slice.data);
  defer std::c::free(data);
  name := dirname(data);
  if !name {
    return None();
  }
  return Some(String::from_ptr(name));
}

struct DIR;
extern fn getcwd(buf: c_str, size: u64) -> c_str;


struct dirent {
  d_ino: u64,
  d_off: u64,
  d_reclen: u16,
  d_type: u8,
  d_name: [u8; 256],
}

extern fn opendir(name: *const u8) -> *mut DIR;
extern fn readdir(dir: *mut DIR) -> *const dirent;
extern fn closedir(dir: *const void) -> s32;

struct Directory {
  path: String,
  entries: List!<String>,
}

impl Directory {
  fn delete!<PathT>(path: PathT, recursive: bool = false, force: bool = false) -> bool where PathT: AsSlice!<u8> {
    slice := path.as_slice();
    if slice.length == 0 {
      return false;
    }

    stat_opt := Stat::new(slice);
    if stat_opt.is_none() {
      return false;
    }
    stat := stat_opt.unwrap();

    if stat.is_file() || stat.is_symlink() {
      extern fn remove(path: *const u8) -> s32;
      if force {
        extern fn chmod(path: *const u8, mode: u32) -> s32;
        chmod(slice.data, 511);
      }
      return remove(slice.data) == 0;
    }

    if stat.is_dir() {
      if recursive {
        dir_opt := Directory::open(slice);
        if dir_opt.is_none() {
          return false;
        }
        dir := dir_opt.unwrap();
        for entry in dir.entries {
          if entry.as_str() == "." || entry.as_str() == ".." {
            continue;
          }
          mut entry_path := String::from(slice);
          entry_path.push('/');
          entry_path.append(entry);
          Directory::delete(entry_path, true, force);
        }
      }
      extern fn rmdir(path: *const u8) -> s32;
      return rmdir(slice.data) == 0;
    }

    return false;
  }

  fn current() -> String {
    buffer: [u8; 256];
    getcwd(buffer, 256);
    return String::from_ptr(buffer);
  }

  fn open!<T>(path: T) -> Option!<Directory> where T: AsSlice!<u8> {
    dir := opendir(path.as_slice().data);
    if !dir => Option!<Directory>::None;

    mut entries: List!<String>;
    mut entry: *mut void;

    while entry = readdir(dir) {
      ent : *const dirent = entry;
      entries.push(String::from_ptr(ent.d_name));
    }

    closedir(dir);

    return Option!<Directory>::Some(.{
      path: String::from(path), 
      entries: entries
    });
  }

  fn files(*const self) -> List!<String> {
    mut files: List!<String>;
    for entry in self.entries {
      file_stat := Stat::new(entry);
      if file_stat.is_none() then continue;
      file := file_stat.unwrap();
      if file.is_file() {
        files.push(entry.clone());
      }
    }
    return files;
  }

  fn subdirectories(*const self) -> List!<String> {
    mut files: List!<String>;
    for entry in self.entries {
      dir_stat := Stat::new(entry);
      if dir_stat.is_none() then continue;
      dir := dir_stat.unwrap();
      if dir.is_dir() {
        files.push(entry.clone());
      }
    }
    return files;
  }

  fn create!<PathT>(path: PathT, create_parent_dirs: bool = false) -> bool where PathT: AsSlice!<u8> {
    mut dir_path := path.as_slice();

    if dir_path.length == 0 {
      return false;
    }

    const DEFAULT_MODE := 493;

    slice := dir_path.as_slice();

    if !create_parent_dirs {
      return mkdir(slice.data, DEFAULT_MODE) == 0;
    }

    if slice.length as u64 >= PATH_MAX as u64 {
      return false;
    }

    mut buf: [u8; PATH_MAX];
    mut i: u64 = 0;

    while i < slice.length {
      if slice[i] == '/' && i != 0 {
        mut j: u64 = 0;
        while j < i {
          buf[j] = slice[j];
          j += 1;
        }
        buf[i] = 0; 
        // >
        // attempt to create prefix (ignore errors, e.g. already exists)
        mkdir(buf, DEFAULT_MODE);
      }
      i += 1;
    }

    return mkdir(slice.data, DEFAULT_MODE) == 0;
  }
}

impl Destroy for Directory {
  fn destroy(*mut self, recursive: bool = true) {
    self.path.destroy();
    self.entries.destroy();
  }
}
