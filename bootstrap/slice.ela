
#ifndef FREESTANDING {
  impl!<T> Clone for Slice!<T> {
    clone :: fn(self) -> #self {
      data : *mut T = malloc(sizeof(T) * self.length);
      memcpy(data, self.data, self.length * sizeof(T));
      return .{
        data: data,
        length: self.length,
      };
    }
  }
}

impl!<T> Subscript!<T> for Slice!<T> {
  subscript :: fn(*const self, idx: u64) -> *const T {
    return &const self.data[idx];
  }
  subscript_mut :: fn(*mut self, idx: u64) -> *mut T {
    return &mut self.data[idx];
  }
}

impl!<T> Slice!<T> {
  from :: fn(data: *const T, len: u32) -> #self => .{
    data: data,
    length: len,
  }
  
  // TODO: do we want to truncate or throw an error when the requested range's length is greater than the amount of 
  // data available??
  slice :: fn!<Range>(self, mut range: Range) -> Slice!<T> where Range: RangeBase!<u8>  |  RangeBase!<u16> |  RangeBase!<u32> | RangeBase!<u64> | RangeBase!<s8>  | RangeBase!<s16> | RangeBase!<s32> |  RangeBase!<s64> {
    if range.begin < 0 then range.begin = 0;

    if range.end == -1 || range.end > self.length {
      range.end = self.length;  
    }

    length := range.end - range.begin;
    
    // TODO: support reverse ranges?
    if length < 0 then return .{};

    return .{
      data: self.data + range.begin,
      length: length,
    }
  }
}

impl!<T> Iterable for Slice!<T> {
  iter :: fn(*const self) -> Iter!<T> {
    return .{
      ptr: self.data as *mut T,
      end: self.data + self.length
    }
  }

  iter_mut :: fn(*mut self) -> Iter!<T> {
    return .{
      ptr: self.data as *mut T,
      end: self.data + self.length
    }
  }
}

impl AsByteSlice for Slice!<u8> {
  as_byte_slice :: fn(self) -> #self {
    return self;
  }
}
