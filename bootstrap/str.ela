impl StringUtils for str {}

impl str {
  fn to_string!<Alloc>(self, allocator: Alloc) -> String where Alloc: Allocator | dyn Allocator {
    return .{
      data: allocator.copy(self.data, sizeof(u8), self.length),
      length: self.length,
    };
  }
}

impl Index!<u8> for str {
  fn index(*const self, idx: u64) -> *const u8 {
    return &const self.data![idx];
  }
  fn index_mut(*mut self, idx: u64) -> *mut u8 {
    return &mut self.data![idx];
  }
}

impl str {
  fn slice(*const self, range: Range) -> [u8] {
    start, end := (range.begin, range.end);
    length := end - start;

    if start < 0 || length < 0 || start + length > self.length {
      return .{};
    }
    
    return .{
      data: self.data + start,
      length: length,
    };
  }
}

// This iterator is over the bytes.
impl Iterable for str { 
  fn iter(*const self) -> Iter!<u8> {
    return .{
      ptr: self.data as *mut u8,
      end: self.data + self.length,
    }
  }
  fn iter_mut(*mut self) -> IterMut!<u8> {
    return .{
      ptr: self.data as *mut u8,
      end: (self.data + self.length),
    }
  }
}

impl Eq for str {
  fn eq(self, other: #self) -> bool {
    if self.length != other.length then return false;
    for idx in 0..self.length {
      if self[idx] != other[idx] then return false;
    }
    return true;
  }
  
  fn neq(self, other: #self) -> bool {
    return !self.eq(other);
  }
}

impl Hash for str {
	fn hash(self) -> u64 {
		mut hash : u64 = 0xCBF29CE484222325;
		for byte in self {
			hash ^= byte;
			hash *= 0x100000001B3;
		}
		return hash;
	}
}

impl AsCharSlice for str {
  fn as_char_slice(self) -> [u32] {
    return .{
      data: self.data,
      length: self.length,
    }
  }
}

impl AsByteSlice for str {
  fn as_byte_slice(self) -> [u8] {
    return .{
      data: self.data,
      length: self.length
    }
  }
}
